buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

if (System.getenv("BUILD_NUMBER") != null) {
    version = "${minecraft_version}-${mod_version}+build${System.getenv("BUILD_NUMBER")}"
} else {
    version = "${minecraft_version}-${mod_version}-SNAPSHOT"
}

group = "com.gildedgames"

def commonManifest = {
    attributes 'Maven-Artifact': "${group}:${archivesBaseName}:${version}"
    attributes 'FMLAT': 'orbis-lib_at.cfg'
}

jar {
    classifier = 'universal'

    from sourceSets.main.output

    manifest commonManifest
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'

    manifest commonManifest

    from sourceSets.main.allSource
}

task deobfJar(type: Jar, dependsOn: reobfJar) {
    classifier = 'dev'

    manifest commonManifest

    from sourceSets.main.output
}

minecraft {
    version = "$minecraft_version-$forge_version"
    mappings = "$forge_mappings"

    runDir = "run"

    makeObfSourceJar = false
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

publishing {
    publications {
        mod(MavenPublication) {
            artifact deobfJar
            artifact sourcesJar

            from components.java
        }
    }

    repositories {
        def mavenSecretsPath = System.getenv("MAVEN_SECRETS_FILE")

        if (mavenSecretsPath != null) {
            def mavenSecretsFile = file(mavenSecretsPath).newReader()
            def mavenSecrets = new Properties()
            mavenSecrets.load(mavenSecretsFile)

            maven {
                url = mavenSecrets.publishUrl

                credentials {
                    username = mavenSecrets.username
                    password = mavenSecrets.password
                }
            }
        }
    }
}

if (System.getenv("JARSIGN_SECRETS_FILE") != null) {
    def secretsFile = file(System.getenv("JARSIGN_SECRETS_FILE"))

    def secrets = new Properties()
    secrets.load(secretsFile.newReader())

    [jar, sourcesJar, deobfJar].each { jarTask ->
        def task = task(jarTask.name + 'Sign', type: SignJar) {
            keyStore = System.getenv("JARSIGN_KEYSTORE_FILE")
            alias = secrets.alias
            storePass = secrets.storePassword
            keyPass = secrets.keyPassword

            inputFile = jarTask.archivePath
            outputFile = jarTask.archivePath
        }

        build.dependsOn task
    }
}

build.dependsOn deobfJar, sourcesJar